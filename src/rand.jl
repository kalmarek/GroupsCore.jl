import Random

"""
    PRASampler
Product Replacement Algorithm sampler for arbitrary group generated by
an explicit finite set of generators.
"""
mutable struct PRASampler{T} <: Random.Sampler{T}
    gentuple::Vector{T}
    right::T
    left::T
end

# constants taken from GAP
function PRASampler(
    G,
    n::Integer = 2ngens(G) + 10,
    scramble_time::Integer = 10max(n, 10),
)
    return PRASampler(Random.default_rng(), G, n, scramble_time)
end

function Random.Sampler(
    RNG::Type{<:Random.AbstractRNG},
    G::Group,
    repetition::Random.Repetition = Val(Inf),
)
    return PRASampler(RNG(), G)
end

function PRASampler(
    rng::Random.AbstractRNG,
    G::Group,
    n::Integer = 2ngens(G) + 10,
    scramble_time::Integer = 10max(n, 10),
)
    if istrivial(G)
        return PRASampler(fill(one(G), n), one(G), one(G))
    end
    @assert hasgens(G)
    l = max(n, 2ngens(G), 2)
    sampler = let S = gens(G)
        S = union!(inv.(S), S)
        append!(S, rand(rng, S, l - length(S)))
        PRASampler(S, one(G), one(G))
    end
    for _ in 1:scramble_time
        _ = rand(rng, sampler)
    end
    return sampler
end

function Random.rand(rng::Random.AbstractRNG, pra::PRASampler)
    range = 1:length(pra.gentuple)

    pra.right = pra.right * rand(rng, pra.gentuple)

    i = rand(rng, range)
    @inbounds pra.gentuple[i] = pra.gentuple[i] * pra.right

    j = rand(rng, range)
    @inbounds pra.left = pra.left * pra.gentuple[j]

    return pra.left
end
